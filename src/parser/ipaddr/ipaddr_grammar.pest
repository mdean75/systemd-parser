ip_a_out = { SOI ~ interface+ ~ EOI }

WHITESPACE = _{ " " }

interface = { index ~ ":" ~ if_name ~ ":" ~ flags ~ mtu ~ qdisc ~ master? ~ state ~ group ~ qlen? ~ NEWLINE ~ link ~ NEWLINE ~ ((inet6 | inet | alt_name) ~ NEWLINE)+ }

index = { ASCII_DIGIT+ }
if_name = { (ASCII_ALPHANUMERIC | "@")+ }
flags = { "<" ~ (flag ~ ("," ~ flag)*) ~ ">" }
flag = { (ASCII_ALPHA_UPPER | CONNECTOR_PUNCTUATION | "-")+ }
mtu = { "mtu" ~ ASCII_DIGIT+ }
qdisc = @{ "qdisc" ~ SPACE_SEPARATOR ~ (ASCII_ALPHANUMERIC | CONNECTOR_PUNCTUATION)+ }
state = @{ "state" ~ SPACE_SEPARATOR ~ ASCII_ALPHA_UPPER+ }
group = @{ "group" ~ SPACE_SEPARATOR ~ ASCII_ALPHANUMERIC+ }
qlen = @{ "qlen" ~ SPACE_SEPARATOR ~ ASCII_DIGIT+ }
link = { link_type ~ address ~ broadcast ~ ("link-netnsid" ~ ASCII_DIGIT+)? }
link_type = @{ "link/" ~ ASCII_ALPHA+ }
address = @{ (ASCII_ALPHANUMERIC | ":")+ }
broadcast = { "brd" ~ address }
master = @{ "master" ~ SPACE_SEPARATOR ~ ASCII_ALPHANUMERIC+ }
alt_name = @{ "altname" ~ " " ~ ASCII_ALPHANUMERIC+ }
inet = { "inet" ~ ip ~ net_prefix ~ broadcast4? ~ scope ~ conf_flag? ~ label? ~ NEWLINE ~ lifetime }
inet6 = { "inet6" ~ ip6 ~ net_prefix ~ scope ~ conf_flag? ~ label? ~ NEWLINE ~ lifetime}

broadcast4 = { "brd" ~ ip }
ip = { ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} ~ "." ~ ASCII_DIGIT{1,3} }
ip6 = { (ASCII_ALPHANUMERIC | ":")+ }
net_prefix = { "/" ~ ASCII_DIGIT{1,3} }
scope = { scope_identifier ~ scope_value }
scope_identifier = { "scope" }
scope_value = @{ ASCII_ALPHANUMERIC+ }
conf_flag = { "noprefixroute" | "home" | "mngtmpaddr" | "nodad" | "optimistic" | "autojoin" }
label = { ASCII_ALPHANUMERIC+ }
lifetime = @{ "valid_lft forever preferred_lft forever" }
